def call(Map configMap){
    pipeline {
        agent { node { label 'roboshop' } }
        // developers can choose to deploy or not
        parameters {
            booleanParam(name: 'deploy', defaultValue: false, description: 'Deploy the application after build')
        }
        environment {
            appVersion = ""
            ACC_ID = "160885265516"
            PROJECT = configMap.get("project")
            COMPONENT = configMap.get("component")
            region = "us-east-1"
        }
        stages {
            stage('test') {
                steps {
                    sh """
                        echo "triggering test: ${configMap.project}"
                    """
                }
            }
            stage('Read version'){
                steps {
                    script {
                        // Load and parse the JSON file
                        def packageJson = readJSON file: 'package.json'
                        
                        // Access fields directly
                        appVersion = packageJson.version
                        echo "Building version ${appVersion}"
                    }
                }
            }
            // THis is for VM
            /* stage('Install Dependencies') {
                steps {
                    sh 'npm install'
                }
            }*/

            stage('Unit Tests') {
                steps {
                    script {
                        def testResult = sh(script: 'npm test', returnStatus: true)
                        if (testResult != 0) {
                            utils.updateCommitStatus('failure', 'Unit tests failed', 'unit-tests')
                            error "Unit tests failed."
                        } else {
                            utils.updateCommitStatus('success', 'Unit tests passed', 'unit-tests')
                        }
                    }
                }
            }

            stage('SonarQube Analysis'){
                steps {
                    script {
                        def scannerHome = tool name: 'sonar-8'
                        withSonarQubeEnv('sonar-server') {
                            sh "${scannerHome}/bin/sonar-scanner"
                        }
                    }
                }
            }

            stage('Quality Gate') {
                steps {
                    script {
                        timeout(time: 1, unit: 'HOURS') {
                            def qg = waitForQualityGate()
                            if (qg.status != 'OK') {
                                utils.updateCommitStatus('failure', "SonarQube quality gate failed: ${qg.status}", 'sonar-scan')
                                error "Quality gate failed: ${qg.status}"
                            } else {
                                utils.updateCommitStatus('success', 'SonarQube quality gate passed', 'sonar-scan')
                            }
                        }
                    }
                }
            } 
            stage('Dependabot Security Check') {
                steps {
                    script {
                        withCredentials([string(credentialsId: 'GITHUB_TOKEN_SCAN', variable: 'GITHUB_TOKEN_SCAN')]) {
                            def repoUrl = sh(script: 'git remote get-url origin', returnStdout: true).trim()
                            def repoPath = repoUrl.replaceAll(/.*github\.com[\/:]/, '').replaceAll(/\.git$/, '')

                            def alertCount = sh(
                                script: """
                                    curl -sf \
                                        -H "Authorization: Bearer \$GITHUB_TOKEN_SCAN" \
                                        -H "Accept: application/vnd.github+json" \
                                        -H "X-GitHub-Api-Version: 2022-11-28" \
                                        "https://api.github.com/repos/${repoPath}/dependabot/alerts?state=open&per_page=100" \
                                    | jq '[.[] | select(.security_vulnerability.severity == "high" or .security_vulnerability.severity == "critical")] | length'
                                """,
                                returnStdout: true
                            ).trim()

                            if (alertCount.toInteger() > 0) {
                                utils.updateCommitStatus('failure', "${alertCount} HIGH/CRITICAL Dependabot alert(s) detected", 'library-scan')
                                error("Build aborted: ${alertCount} HIGH/CRITICAL Dependabot alert(s) detected. Resolve them before proceeding.")
                            }
                            utils.updateCommitStatus('success', 'Dependabot check passed — no HIGH/CRITICAL alerts', 'library-scan')
                            echo "Dependabot check passed — no HIGH or CRITICAL vulnerabilities found."
                        }
                    }
                }
            }
        
            stage('Build Image') {
                steps {
                    script{
                        // Commands here have AWS authentication
                        sh """
                            docker build -t ${ACC_ID}.dkr.ecr.${region}.amazonaws.com/${PROJECT}/${COMPONENT}:${appVersion} .
                        """
                    }
                }
            }
            stage('Trivy OS Scan') {
                steps {
                    script {
                        // Generate table report
                        sh """
                            trivy image \
                                --scanners vuln \
                                --pkg-types os \
                                --severity HIGH,MEDIUM \
                                --format table \
                                --output trivy-os-report.txt \
                                --exit-code 0 \
                                ${ACC_ID}.dkr.ecr.${region}.amazonaws.com/${PROJECT}/${COMPONENT}:${appVersion}
                        """

                        // Print table to console
                        sh 'cat trivy-os-report.txt'

                        // Fail pipeline if vulnerabilities found
                        def scanResult = sh(
                            script: """
                                trivy image \
                                    --scanners vuln \
                                    --pkg-types os \
                                    --severity HIGH,MEDIUM \
                                    --format table \
                                    --exit-code 1 \
                                    --quiet \
                                    ${ACC_ID}.dkr.ecr.${region}.amazonaws.com/${PROJECT}/${COMPONENT}:${appVersion}
                            """,
                            returnStatus: true
                        )

                        if (scanResult != 0) {
                            utils.updateCommitStatus('failure', 'Trivy OS scan: HIGH/MEDIUM vulnerabilities found', 'trivy-scan')
                            error "🚨 Trivy found HIGH/MEDIUM OS vulnerabilities. Pipeline failed."
                        } else {
                            utils.updateCommitStatus('success', 'Trivy OS scan passed — no HIGH/MEDIUM vulnerabilities', 'trivy-scan')
                            echo "✅ No HIGH or MEDIUM OS vulnerabilities found. Pipeline continues."
                        }
                    }
                }
            }
            stage('Trivy Dockerfile Scan') {
                steps {
                    script {
                        sh """
                            trivy config \
                                --severity HIGH,MEDIUM \
                                --format table \
                                --output trivy-dockerfile-report.txt \
                                Dockerfile
                        """

                        sh 'cat trivy-dockerfile-report.txt'

                        def scanResult = sh(
                            script: """
                                trivy config \
                                    --severity HIGH,MEDIUM \
                                    --exit-code 1 \
                                    --format table \
                                    Dockerfile
                            """,
                            returnStatus: true
                        )

                        if (scanResult != 0) {
                            error "🚨 Trivy found HIGH/MEDIUM misconfigurations in Dockerfile. Pipeline failed."
                        } else {
                            echo "✅ No HIGH or MEDIUM Dockerfile misconfigurations found. Pipeline continues."
                        }
                    }
                }
            }
            stage ('Push image to ECR'){
                steps {
                    script {
                        try {
                            withAWS(credentials: 'aws-creds', region: "${region}") {
                                sh """
                                    aws ecr get-login-password --region ${region} | docker login --username AWS --password-stdin ${ACC_ID}.dkr.ecr.us-east-1.amazonaws.com
                                    docker push ${ACC_ID}.dkr.ecr.${region}.amazonaws.com/${PROJECT}/${COMPONENT}:${appVersion}
                                """
                            }
                            utils.updateCommitStatus('success', "Image ${appVersion} pushed to ECR", 'push-image')
                        } catch (err) {
                            utils.updateCommitStatus('failure', 'Failed to push image to ECR', 'push-image')
                            throw err
                        }
                    }
                }
            }
            stage('Deploy') {
                when {
                    expression { params.deploy == true }
                }
                steps {
                    script{
                        withAWS(region:"${region}",credentials:'aws-creds') {
                            sh """
                                cd helm
                                set -e
                                aws eks update-kubeconfig --region ${region} --name ${PROJECT}-dev
                                kubectl get nodes
                                sed -i "s/IMAGE_VERSION/${appVersion}/g" values.yaml
                                helm upgrade --install ${COMPONENT} -f values-dev.yaml -n ${PROJECT} --atomic --wait --timeout=5m .
                                #kubectl apply -f ${COMPONENT}-dev.yaml
                            """
                        }
                    }
                }
            }

            stage('Run Component Tests') {
                when {
                    expression { params.deploy == true }
                }
                steps {
                    script {
                        def testJob = "${PROJECT}/${COMPONENT}-tests"
                        echo "Triggering test pipeline: ${testJob}"
                        def result = build(
                            job: testJob,
                            wait: true,
                            propagate: false
                        )
                        if (result.result != 'SUCCESS') {
                            utils.updateCommitStatus('failure', "Functional tests failed — see ${result.absoluteUrl}", 'dev-deploy')
                            error("${COMPONENT} tests failed — deploy marked as failure. Check ${result.absoluteUrl} for details.")
                        }
                        utils.updateCommitStatus('success', 'Functional tests passed — deploy successful', 'dev-deploy')
                        echo "${COMPONENT} tests passed."
                    }
                }
            }
        }
        post {
            success {
                echo "Pipeline succeeded on branch: ${env.BRANCH_NAME}"
            }
            failure {
                echo "Pipeline failed on branch: ${env.BRANCH_NAME}"
            }
        }
    }
}
